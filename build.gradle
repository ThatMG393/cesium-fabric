plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.8.0'
	id 'com.github.johnrengelman.shadow' version '8.0.0'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
	maven {
		url = "https://oss.sonatype.org/content/repositories/snapshots"
	}
}

loom {
	accessWidenerPath = file("src/main/resources/cesium.accesswidener")
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	shadow(implementation("com.github.luben:zstd-jni:1.5.5-2"))
	shadow(implementation("org.lmdbjava:lmdbjava:0.9.0-SNAPSHOT"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

remapJar {
	dependsOn(shadowJar)
	inputFile = file("build/libs/shadow-tmp.jar")
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveFileName = "shadow-tmp.jar"
}
